/*
 * SPDX-License-Identifier: GPL-3.0-or-later
 * SPDX-FileCopyrightText: 2026 Leonhard Kargl <leo.kargl@proton.me>
 */

/**
 * The base class for all files. All files are singletons i.e. multiple calls
 * to {@link get_for_uri} will return the same object at any given time.
 * This is the only way to obtain files. It automatically constructs the correct file
 * (directory or document).
 *
 * A FileBase file is backed by a {@link GLib.File} that will not change during its lifetime.
 * Some properties are always valid (like of course the file, uri and also basename) while some
 * are only valid after the file has been loaded with {@link load}.
 */
public abstract class Quicknote.FileBase : Object {
    private static HashTable<string, unowned FileBase> known_files;

    public static void init () {
        known_files = new HashTable<string, unowned FileBase> (str_hash, str_equal);
    }

    public static FileBase? get_for_info (string parent, FileInfo info) {
        File parent_file = File.new_for_uri (parent);

        var file = parent_file.get_child (info.get_name ());

        if (file.get_uri () in known_files) {
            return known_files[file.get_uri ()];
        }

        if (info.get_file_type () == DIRECTORY) {
            return new Directory (file, info);
        } else {
            return new NoteFile (file, info);
        }
    }

    public static async FileBase? get_for_path (string path) {
        try {
            var uri = Filename.to_uri (path, null);
            return yield get_for_uri (uri);
        } catch (Error e) {
            warning ("Error converting path to URI: %s", e.message);
        }

        return null;
    }

    public static async FileBase? get_for_uri (string uri) {
        if (uri in known_files) {
            return known_files[uri];
        }

        var file = File.new_for_uri (uri);

        FileInfo info;
        try {
            info = yield file.query_info_async ("standard::*", NONE, Priority.DEFAULT, null);
        } catch (Error e) {
            warning ("Error querying file info: %s", e.message);
            return null;
        }

        if (info.get_file_type () == DIRECTORY) {
            return new Directory (file, info);
        } else {
            return new NoteFile (file, info);
        }
    }

    public Cancellable cancellable { get; construct; }

    /**
     * The backing file of #this. It is discouraged to use this directly.
     * It is only provided for occasions where it's really needed, e.g. when communicating
     * with third parties (dnd, c&p) or doing operations. Instead you should use the properties of #this
     * like {@link display_name}, {@link uri}, {@link size}, etc.
     */
    public File file { get; construct; }

    private FileInfo? _info;
    public FileInfo info {
        protected get { return _info; }
        protected construct set {
            _info = value;
            display_name = _info.get_display_name ();
            icon = _info.get_icon () ?? new ThemedIcon ("text-x-generic");
        }
    }

    // Properties of the file that are always valid. Note they may change during the lifetime of #this
    public string uri { get; private set; }
    public string display_name { get; private set; }
    public Icon icon { get; private set; }

    // Misc stuff
    public bool move_queued { get; set; default = false; }

    private bool refreshing = false;
    private uint loaded = 0;
    private uint unload_timeout_id = 0;

    protected FileBase () {}

    construct {
        uri = _file.get_uri ();

        known_files[uri] = this;

        cancellable = new Cancellable ();
    }

    ~FileBase () {
        cancellable.cancel ();
        known_files.remove (uri);
    }

    protected virtual async void load_internal () { }
    protected virtual async void unload_internal () { }

    public void load () {
        if (unload_timeout_id != 0) {
            Source.remove (unload_timeout_id);
            unload_timeout_id = 0;
        } else if (loaded == 0 && !refreshing) {
            load_internal.begin ();
        }

        loaded++;
    }

    public void queue_unload () {
        if (loaded == 0 || unload_timeout_id != 0) {
            critical ("Unload called on unloaded file %s so to often", uri);
            return;
        }

        loaded--;

        if (loaded == 0 && !refreshing) {
            unload_timeout_id = Timeout.add_seconds (5, unload_timeout_func);
        }
    }

    private bool unload_timeout_func () {
        unload_internal.begin ();
        unload_timeout_id = 0;
        return Source.REMOVE;
    }

    public async void refresh () {
        refreshing = true;

        if (unload_timeout_id != 0) {
            Source.remove (unload_timeout_id);
            unload_timeout_id = 0;
            yield unload_internal ();
        } else if (loaded > 0) {
            yield unload_internal ();
        }

        try {
            info = yield file.query_info_async ("standard::*", NONE, Priority.DEFAULT, cancellable);
        } catch (Error e) {
            warning ("Error refreshing file info: %s", e.message);
        }

        if (loaded > 0) {
            yield load_internal ();
        }

        refreshing = false;
    }
}
